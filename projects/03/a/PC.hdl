// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // convert reset, load, inc to 2-bits
    Or(a=reset, b=load, out=sel1);
    Not(in=load, out=notload);
    And(a=notload, b=inc, out=niand);
    Or(a=reset, b=niand, out=sel0);

    // inc
    Inc16(in=regout, out=incout);

    // select by control bit
    Mux4Way16(a=regout, b=incout, c=in, d=false, sel[0]=sel0, sel[1]=sel1, out=regin);

    // load bit for Registor
    Or(a=sel0, b=sel1, out=orout1);
    Register(in=regin, load=orout1, out=regout, out=out);
}
